apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing{
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'A library provides Composables that handle nice and smooth zooming behaviour for you'
                    url = 'https://github.com/Mr-Pine/AndroidUtilityLibraries/tree/master/libraries/zoomables'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://github.com/Mr-Pine/AndroidUtilityLibraries/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'Mr-Pine'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/Mr-Pine/AndroidUtilityLibraries.git'
                        developerConnection = 'scm:git:ssh://github.com/Mr-Pine/AndroidUtilityLibraries.git'
                        url = 'https://github.com/Mr-Pine/AndroidUtilityLibraries/tree/master/libraries/zoomables'
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    /*useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.secretKeyRingFile"],
            rootProject.ext["signing.password"],
    )*/
    sign publishing.publications
}